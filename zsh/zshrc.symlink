# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# load bash profile
source ~/.bash_profile

# Ensure $HOME bin directories exists (.local/bin is preferred by pipx)
[ -d "$HOME/bin" ] || mkdir -p "$HOME/bin"
[ -d "$HOME/.local/bin" ] || mkdir -p "$HOME/.local/bin" 

# Add $HOME bins to the PATH - run this last to ensure this dir is always
# preferred
export PATH="$HOME/bin:$PATH"
export PATH="$HOME/.local/bin:$PATH"
